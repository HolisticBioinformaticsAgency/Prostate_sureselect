// ----------------- Helpers -----------------
def get_cluster_options(task_time) {
    def partition = (task_time <= 4.h) ? 'genomics' : 'comp'
    def qos       = (partition == 'genomics') ? 'genomics' : 'normal'
    return "--partition=${partition} --qos=${qos}"
}

// Linear backoff: 1.0×, 2.0×, 2.5×, 3.0× ...
def time_mod(base, attempt)   { (attempt == 1) ? base : (base + (attempt * (base / 2))) }
def memory_mod(base, attempt) { (attempt == 1) ? base : (base + (attempt * (base / 2))) }

// ----------------- Caps -----------------
params {
  max_memory = 330.GB
  max_cpus   = 48
  max_time   = 7.d
}

// ----------------- Singularity/Apptainer -----------------
singularity {
  enabled    = true
  autoMounts = true
  cacheDir   = "${baseDir}/containers"
  runOptions = '-B /scratch -B /projects -B /fs04'
}

// ----------------- Slurm executor tuning -----------------
executor {
  $slurm {
    queueSize         = 200
    pollInterval      = '30 sec'
    queueStatInterval = '10m'
  }
}

// ----------------- Process defaults -----------------
process {
  executor      = 'slurm'
  stageInMode   = 'symlink'
  cache         = 'lenient'
  beforeScript  = 'module load singularity'
  errorStrategy = 'retry'
  maxRetries    = 3
  scratch       = true

  // DEFAULT resources (non-heavy hitters)
  cpus   = { 2 }
  memory = { [ memory_mod(8.GB,   task.attempt), params.max_memory ].min() }
  time   = { [ time_mod(1.h,      task.attempt), params.max_time   ].min() }

  // Dynamic partition/QoS selection (recomputed each attempt)
  clusterOptions = { get_cluster_options(task.time) }

  // ---- Heavy hitters ----
  withName: /^(ALIGN_AND_SORT|VARDICT.*|CNVKIT_BATCH|MSIPRO.*|POLYSOLVER)$/ {
    cpus   = { 8 }
    memory = { [ memory_mod(16.GB, task.attempt), params.max_memory ].min() }
    time   = { [ time_mod(2.h,     task.attempt), params.max_time   ].min() }
  }

  // ---- Tool-specific binds that autoMounts won't create ----
 withName: 'VEP_ANNOTATE' {
    containerOptions = "--bind /fs04/scratch2/cg90/liem/resources/refdata/vep_cache:/vep_cache"
  }
withName: 'SNPEFF_ANNOTATE' {
    containerOptions = "--bind /fs04/scratch2/cg90/liem/resources/refdata/snpEff:/snpeff_data"
  }
}
